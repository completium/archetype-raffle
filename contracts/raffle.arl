archetype raffle(
  owner        : address,
  jackpot      : tez,
  ticket_price : tez
)

variable open_buy     : option<date> = none
variable close_buy    : option<date> = none

variable chest_time   : option<nat>  = none
variable reveal_fee   : option<rational> = none

asset player {
  id                 : address;
  locked_raffle_key  : chest;
  revealed           : bool = false;
}

variable raffle_key  : nat = 0

states =
| Created initial
| Initialised
| Transferred

transition initialise(ob : date, cb : date, t : nat, rf : rational) {
  called by owner
  require {
    r0 : now <= ob < cb         otherwise "INVALID_OPEN_CLOSE_BUY";
    r1 : rf <= 1                otherwise "INVALID_REVEAL_FEE";
    r2 : transferred = jackpot  otherwise "INVALID_AMOUNT"
  }
  from Created to Initialised
  with effect {
    open_buy      := some(ob);
    close_buy     := some(cb);
    chest_time    := some(t);
    reveal_fee    := some(rf)
  }
}

entry buy (lrk : chest) {
  state is Initialised
  constant {
    value_open_buy ?is open_buy otherwise "ERROR";
    value_close_buy ?is close_buy otherwise "ERROR";
  }
  require {
    r3 : transferred = ticket_price               otherwise "INVALID_TICKET_PRICE";
    r4 : value_open_buy < now < value_close_buy   otherwise "RAFFLE_CLOSED"
  }
  effect { player.add({ id = caller; locked_raffle_key = lrk }) }
}

entry reveal(addr : address, k : chest_key) {
  state is Initialised
  constant {
    value_close_buy ?is close_buy otherwise "ERROR";
    value_chest_time ?is chest_time otherwise "ERROR";
    value_reveal_fee ?is reveal_fee otherwise "ERROR";
    value_player ?is player[addr] otherwise "PLAYER_NOT_FOUND";
  }
  require {
    r5 : value_close_buy < now   otherwise "RAFFLE_OPEN";
    r6 : not value_player.revealed  otherwise "PLAYER_ALREADY_REVEALED"
  }
  effect {
    match open_chest(k, value_player.locked_raffle_key, value_chest_time) with
    | some (unlocked) -> begin
        match unpack<nat>(unlocked) with
        | some(partial_key) ->
          raffle_key += partial_key;
          player[addr].revealed := true
        | none -> player.remove(addr)
        end
      end
    | none -> fail("INVALID_CHEST_KEY")
    end;
    transfer (value_reveal_fee * ticket_price) to caller;
  }
}

transition %transfer() {
  require {
    r7: player.select(the.revealed).count() = player.count() otherwise "EXISTS_NOT_REVEALED"
  }
  from Initialised to Transferred
  with effect {
    const dest ?= player.nth(raffle_key % player.count()) : "ERROR";
    transfer balance to dest;
  }
}

