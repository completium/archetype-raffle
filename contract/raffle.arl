archetype raffle(
  owner        : address,
  jackpot      : tez,
  ticket_price : tez
)

variable open_buy     : option<date> = none
variable close_buy    : option<date> = none
variable close_reveal : option<date> = none

variable chest_time   : option<nat>  = none
variable reveal_fee   : option<rational> = none

asset player {
  id                 : address;
  locked_raffle_key  : chest;
  revealed           : bool = false;
}

variable raffle_key  : nat = 0

states =
| Created initial
| Initialised
| Transferred

transition initialise(ob : date, cb : date, cr : date, t : nat, rf : rational) {
  called by owner
  require {
    r0 : now <= ob < cb < cr       otherwise "INVALID_OPEN_CLOSE_BUY_REVEAL";
    r1 : rf <= 1                   otherwise "INVALID_REVEAL_FEE";
    r2 : transferred = jackpot     otherwise "INVALID_AMOUNT"
  }
  from Created to Initialised
  with effect {
    open_buy     := some(ob);
    close_buy    := some(cb);
    close_reveal := some(cr);
    chest_time   := some(t);
    reveal_fee   := some(rf)
  }
}

entry buy (lrk : chest) {
  state is Initialised
  require {
    r3 : transferred = ticket_price                     otherwise "INVALID_TICKET_PRICE";
    r4 : opt_get(open_buy) < now < opt_get(close_buy)   otherwise "BUY_CLOSED"
  }
  effect { player.add({ id = caller; locked_raffle_key = lrk }) }
}

entry reveal(addr : address, k : chest_key) {
  state is Initialised
  require {
    r5 : opt_get(close_buy) < now < opt_get(close_reveal) otherwise "REVEAL_CLOSED";
    r6 : not player[addr].revealed                        otherwise "PLAYER_ALREADY_REVEALED"
  }
  effect {
    match open_chest(k, player[addr].locked_raffle_key, opt_get(chest_time)) with
    | left (unlocked) -> raffle_key += opt_get(unpack<nat>(unlocked))
    | right(error)    -> fail("INVALID_TIMELOCK")
    end;
    player[addr].revealed := true;
    transfer (opt_get(reveal_fee) * ticket_price) to caller;
  }
}

transition %transfer() {
  require {
    r7: opt_get(close_reveal) + (opt_get(close_buy) - opt_get(open_buy)) < now otherwise "TRANSFER_CLOSED"
  }
  from Initialised to Transferred
  with effect {
    player.removeif(not the.revealed);
    transfer balance to player.nth(raffle_key % player.count());
  }
}

